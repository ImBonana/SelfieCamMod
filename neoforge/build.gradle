plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mc_version_range', project.neoforge_game_version_range
    inputs.property 'loader_version_range', project.neoforge_loader_version_range

    filesMatching('META-INF/neoforge.mods.toml') {
        expand (
                version: project.version,
                mc_version_range: project.neoforge_game_version_range,
                loader_version_range: project.neoforge_loader_version_range
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    exclude "architectury.common.json"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn(shadowJar)
    atAccessWideners.add(loom.accessWidenerPath.get().asFile.name)
}
